command besttype
  'tries to find the best type for the given object'
  object 'object'
  if ( isstring object ) ( switch ( canparseint32 object ) ( toint32 object ) object ) object

command canparseint32
  'tests whether the specified string can be parsed to an int32'
  string 'string'
  Int32.TryParse string 0

command decrement
  'decrements a variable'
  "name" 'name of the variable'
  ( amount 1 ) 'the value subtracted from the original value of the varable'
  set True name ( - ( get name ) amount )

command getmember
  'get the array of member-information for the specified member in an object'
  'an array of "MemberInfo"-values'
  object 'object'
  name 'name of the member'
  invokeinstancemember 'GetMember' ( typeof object ) name

command hasmember
  'tests if the specified member exists in an object'
  'if the object has the specified member, "true" is returned, otherwise "false"'
  object 'object'
  name 'name of the member'
  > ( Length ( getmember object name ) ) 0

command hastype
  'tests if an object has the given type'
  'if the object has the given type, "true" is returned, otherwise "false"'
  object 'object'
  name 'name of the type'
  == ( typename object ) name

command increment
  'increments a variable'
  'the incremented value of the variable'
  'incrementing a string by a string means appending one string to the other'
  "name" 'name of the variable'
  ( amount 1 ) 'the value added to the original value of the variable'
  set True name ( + ( get name ) amount )

command ischar
  'tests if an object is a "char"-value'
  'a "bool"-value'
  'if the object is a "char"-value, "true" is returned, otherwise "false"'
  object 'object'
  hastype object 'Char'

command isempty
  'checks wether an object is isempty'
  'if the object is empty, "true" is returned, otherwise "false"'
  object 'object'
  == ( size object ) 0

command isnull
  'tests, if an object has the value "null"'
  'if the object has the value "null", "true" is returned, otherwise "false"'
  object 'object'
  == object ( null )

command isnullorempty
  string
  String.IsNullOrEmpty string

command isstring
  'tests if an object is a "string"-value'
  'a "bool"-value'
  'if the object is a "string"-value, "true" is returned, otherwise "false"'
  object 'object'
  hastype object String

command iswhitespace
  'checks if a value is white space'
  'if the character is white space, "true" is returned, otherwise "false"'
  value 'value to be checked'
  Char.IsWhiteSpace ( tochar value )

command not
  'negates a truth value'
  'if the value is "false", "true" is returned, otherwise "false"'
  value 'truth value'
  if ( return value ) False True

command notempty
  'checks if an object is not empty'
  'if the object is empty, "false" is returned, otherwise "true"'
  object 'object'
  not ( isempty object )

command quote
  'returns the unevaluated value'
  'the unenvaluated value'
  "value" 'value'
  return value

command size
  'returns the size of an object (e.g. an array, a list, or a string)'
  'the size of the object'
  object 'object'
  if ( hasmember object 'Length' ) ( Length object ) ( Count object )

command sleep
  'sleeps for the given amount of time'
  timeout 'the timeout in milliseconds'
  Thread.Sleep timeout

command tobiginteger
  'converts a number to a BigInteger'
  'a "BigInteger"-value'
  BigInteger.Parse ( tostring value )

command tochar
  'converts the value to "char"-value'
  'a "char"-value'
  'if the value is already a "char"-value, it is returned, otherwise the value is converted to a string and the first character is returned'
  value 'value'
  if ( ischar value ) value ( at ( tostring value ) 0 )

command tocharacters
  'converts a string to an array of characters'
  'an array of characters'
  string 'string'
  ToCharArray string

command todouble
  'converts value to a "double"-value'
  'a double-value'
  value 'number to be converted'
  Convert.ToDouble ( tostring value )

command toint32
  'converts a value to an "int32"-value'
  'an "int32"-value'
  'the value can be a number or a string'
  value 'value to be converted'
  Int32.Parse ( tostring value )

command tostring
  'converts an object to a string'
  'a "string"-value'
  object 'object'
  ToString object

command typename
  'returns the name of the type of an object'
  'the name of the type'
  object 'object'
  invokeinstancemember 'Name' ( typeof object )

command typeof
  'returns the type-object of an object'
  'the type-object'
  object 'object'
  invokeinstancemember 'GetType' object